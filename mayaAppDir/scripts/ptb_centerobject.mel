/* ptb_centerobject.mel

This script may be freely distributed. Modify at your own risk.

Author:		Dirk Bialluch (DirkBi@Software2000.de)

Creation Date:	22.05.98
Last Update:	24.05.99
*/

//
// Main procedure
//

global proc ptb_centerobject ()
{
	string $objList[] = `ptb_filterTransforms`;
	int $count = `size ($objList)`;
	$count -= 1;
	float $piv[3];
	int $bbMode = `optionMenu -q -select ptb12_bb`;

	for ($count; $count >= 0; $count--)
	{
		float $bb[6] = `xform -q -os -boundingBox $objList[$count]`;
		float $diff[3] = {0,0,0};

		if (`radioButtonGrp -q -select ptb12_mode` == 1)
			$piv = `xform -q -ws -sp $objList[$count]`;
		else
			$piv = `xform -q -ws -rp $objList[$count]`;

		if (`checkBox -q -value ptb12_xbox`)
		{
			if ($bbMode == 2)
				$diff[0] = $bb[0] - $piv[0];
			else if ($bbMode == 3)
				$diff[0] = $bb[3] - $piv[0];
			else
				$diff[0] = ($bb[0] + $bb[3]) / 2 - $piv[0];
		}
		if (`checkBox -q -value ptb12_ybox`)
		{
			if ($bbMode == 4)
				$diff[1] = $bb[1] - $piv[1];
			else if ($bbMode == 5)
				$diff[1] = $bb[4] - $piv[1];
			else
				$diff[1] = ($bb[1] + $bb[4]) / 2 - $piv[1];
		}
		if (`checkBox -q -value ptb12_zbox`)
		{
			if ($bbMode == 6)
				$diff[2] = $bb[2] - $piv[2];
			else if ($bbMode == 7)
				$diff[2] = $bb[5] - $piv[2];
			else
				$diff[2] = ($bb[2] + $bb[5]) / 2 - $piv[2];
		}

		if (`radioButtonGrp -q -select ptb12_mode` == 1)
		{
			xform -r -ws -sp $diff[0] $diff[1] $diff[2] $objList[$count];
			if (`checkBox -q -value ptb12_sticky`)
				xform -r -ws -rp $diff[0] $diff[1] $diff[2] $objList[$count];
		}
		else
		{
			xform -r -ws -rp $diff[0] $diff[1] $diff[2] $objList[$count];
			if (`checkBox -q -value ptb12_sticky`)
				xform -r -ws -sp $diff[0] $diff[1] $diff[2] $objList[$count];
		}

		// move object to old pivot position
		xform -r -ws -translation (-$diff[0]) (-$diff[1]) (-$diff[2]) $objList[$count];
	}
}

// end of script
