/*  This file downloaded from Highend3d.com
''  
''  Highend3d.com File Information:
''  
''    Script Name: mPlanarPoints v0.0
''    Author: 
''    Last Updated: March 31, 2005
''    Update/Change this file at:
''    http://www.highend3d.com/maya/mel/?section=modeling#3503
''  
''  Please do not alter any information above this line
''  it is generated dynamically by Highend3d.com and will
''  be changed automatically on any updates.
*/

// Select three CVs first as pins
// any other cvs selected after the first two will be moved 
// onto the plane defined by the pins
// useful for smoothing corners where patches meet 
// by Matthew Gidney 2000 
//
//updated by Mihai Militaru (mihai_dot_militaru_at_gmail_dot_com)-> added window, first select 3 points, get plane that lasts all session, than select all and apply :)
//now works on all points (verts, subdiv verts, etc) and mabye many more :)

global proc mPlanarCvs() {
	if ( `window -exists planSel` == true )
	{
		deleteUI planSel;
	}
	window -s 0 -toolbox true -t "Planarize Points" -tb 1 planSel;
	columnLayout -adj 1;
	textField -ed 0 -tx "...first define plane, then select all and apply" planText;
	button -l "Get Plane" -c "getPlane";
	button -l "Apply Planarize" -c "applyPlanarize";
	showWindow planSel;
	window -e -w 260 -h 88 -te 300 -le 300 planSel;
	}
		
global proc getPlane()
		{
        string $parents[] = `ls -sl -fl`;
        int $numberOfCvs = `size $parents`;
        
        if ( $numberOfCvs != 3) {
                error("mPlanarCvs(): You must pick only 3 Cv's \n");
                return;
        }       
        global float $CVPin1[3];
        global float $CVPin2[3];
        global float $CVPin3[3];
        $CVPin1 = `xform -q -worldSpace -t $parents[0]`;
        $CVPin2 = `xform -q -worldSpace -t $parents[1]`;
        $CVPin3 = `xform -q -worldSpace -t $parents[2]`;
        
        // first define the plane by its normal
        
        vector $pin1To2 = <<($CVPin1[0]-$CVPin2[0]),($CVPin1[1]-$CVPin2[1]),($CVPin1[2]-$CVPin2[2])>>;
        vector $pin1To3 = <<($CVPin1[0]-$CVPin3[0]),($CVPin1[1]-$CVPin3[1]),($CVPin1[2]-$CVPin3[2])>>; 
        global vector $normal;
        global float $magNormal;
        global vector $unitNormal;
        $normal = cross ($pin1To2,$pin1To3);
        $magNormal = mag ($normal);
        $unitNormal = unit ($normal);
		}
 
global proc applyPlanarize()
	{
		$startTime=`timerX`;
		global vector $normal;
        global float $magNormal;
        global vector $unitNormal;
        global float $CVPin1[3];
        global float $CVPin2[3];
        global float $CVPin3[3];
        string $parents[] = `ls -sl -fl`;
        int $numberOfCvs = `size $parents`;
        
        // next resolve the points onto the plane via the normal
        

        for ($cnt = 0; $cnt < ($numberOfCvs); $cnt++) {
                float $point[3] = `xform -q -worldSpace -t $parents[$cnt]`;
                vector $pointToPin1 = <<($point[0]-$CVPin1[0]),($point[1]-$CVPin1[1]),($point[2]-$CVPin1[2])>>;
                float $magPointToPin1 = mag ($pointToPin1);
                float $dotProduct = dot ( $normal,$pointToPin1);
                vector $moveVectorRel = -1.0 * $unitNormal * ($dotProduct / $magNormal);
                
                xform -worldSpace -r -t ($moveVectorRel.x) ($moveVectorRel.y) ($moveVectorRel.z) $parents[$cnt];
        }
		$totalTime=`timerX -startTime $startTime`;
		print ($numberOfCvs+" items processed in "+$totalTime+" seconds");
	}

mPlanarCvs;
